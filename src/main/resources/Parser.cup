package parser;

import ast.*; 
import java.io.*;
import java.util.*;
import java_cup.runtime.Symbol;

parser code {:

	public void syntax_error(Symbol currentToken) {
		report_error("Syntax error at [" + (currentToken.left+1) +"," + (currentToken.right+1) +"]!", null);
	}

	public static Symbol parse(Reader input) throws Exception {
		return (new Parser(new Lexer(input))).parse();
	}

	public static Symbol parse(String input) throws Exception {
		return parse(new StringReader(input));
	}

	public static void main(String[] args) throws Exception {
		if (args.length < 1) {
			System.out.println(parse(new InputStreamReader(System.in, "UTF8")).value);
		} else for (int i = 0; i < args.length; ++i) {
			System.out.println(parse(new InputStreamReader(new FileInputStream(args[i]), "UTF8")).value);
		}
	}

:}
non terminal Programa prog;
non terminal Definicion def;
non terminal Parametro param;
non terminal Expresion paramExp;
non terminal Sentencia stmt;
non terminal Expresion exp;
non terminal AsignacionExpresion assigExp;

terminal Double NUM;
terminal String ID;
terminal String IDFUNC;
terminal String STRING;
terminal FUNCION;
terminal RECIBE;
terminal SIGNO_NO;
terminal PARENTESIS_IZQUIERDO;
terminal PARENTESIS_DERECHO;
terminal SIGNO_MULT;
terminal FALSO;
terminal ENTONCES;
terminal SIGNO_MAS;
terminal SIGNO_MENOS;
terminal PUNTO;
terminal CORCHETE_IZQUIERDO;
terminal VERDADERO;
terminal SIGNO_IGUAL;
terminal CORCHETE_DERECHO;
terminal SIGNO_Y;
terminal SIGNO_MENOR_IGUAL;
terminal SIGNO_MAYOR;
terminal SIGNO_MENOR;

non terminal List<Sentencia> repeat$stmt;
non terminal List<Parametro> repeat$param;
non terminal List<Expresion> repeat$paramExp;
terminal HACER;
terminal NADA;
terminal MIENTRAS;
terminal SINO;
terminal SI;
terminal SIGNO_O;
terminal PONER;
terminal EN;
terminal SIGNO_MAYOR_IGUAL;
terminal LLAMAR;
terminal PONER_EN;
terminal MOSTRAR;
terminal MOSTRAR_LINEA;
terminal LARGO;
terminal DEFINIDA;
terminal Tipo TIPO;
terminal CREAR_VARIABLE;
terminal CREAR_LISTA;
terminal SIM_TIPO;
terminal CANTIDAD;
terminal POSICION;
terminal OBTENER_POSICION;

precedence left FUNCION;
precedence left SIGNO_NO;
precedence left SIGNO_MULT;
precedence left SIGNO_MAS, SIGNO_MENOS;
precedence left SIGNO_Y;
precedence left SIGNO_O;
precedence left SINO;
precedence left SIGNO_IGUAL;
precedence left SIGNO_MENOR_IGUAL;
precedence left SIGNO_MAYOR_IGUAL;
precedence left SIGNO_MENOR;
precedence left SIGNO_MAYOR;

start with prog;

prog ::=
		def:d {:  //System.out.println("" + d);
		RESULT = d; :} 
	|
		stmt:s {: //System.out.println("" + s); 
		RESULT = s; :} 
;

def ::=
		FUNCION:$1 IDFUNC:$2 RECIBE:$3 PARENTESIS_IZQUIERDO:$4 repeat$param:$5 PARENTESIS_DERECHO:$6 param:$7 stmt:$8
		{: RESULT = new Funcion($2,$5.toArray(new Parametro[$5.size()]),$7,$8);
		:}
;
param ::=
		TIPO:$1 ID:$2
		{: RESULT = new Parametro($2,$1); :}
;

paramExp ::=
		exp:a {:  //System.out.println("" + a);
		RESULT = a; :} 
;

stmt ::=
		PONER:$1 exp:$2 EN:$3 ID:$4 PUNTO:$5 
		{: RESULT = new Asignacion($4, $2); :}
	|   PONER:$1 exp:$2 EN:$3 TIPO:$4 ID:$5 PUNTO:$6
		{: RESULT = new DeclaracionIniciar($2,$4,$5); :}	
	|	NADA:$1 PUNTO:$2 
		{: RESULT = new Secuencia(new Sentencia[0]); :}
	|	CORCHETE_IZQUIERDO:$1 repeat$stmt:$2 CORCHETE_DERECHO:$3 
		{: RESULT = new Secuencia($2.toArray(new Sentencia[$2.size()])); :}
	|	MIENTRAS:$1 exp:$2 HACER:$3 stmt:$4 
		{: RESULT = new MientrasHacer($2, $4); :}
	|	SI:$1 exp:$2 ENTONCES:$3 stmt:$4 SINO:$5 stmt:$6 
		{: RESULT = new SiEntoncesSino($2, $4, $6); :}
	|	SI:$1 exp:$2 ENTONCES:$3 stmt:$4
		{: //System.out.println($1+" "+$2+" "+$3+" "+$4);
		RESULT = new SiEntonces($2, $4); :}
	|	LLAMAR:$1 IDFUNC:$2 RECIBE:$3 repeat$paramExp:$4 PONER_EN:$5 ID:$6 PUNTO:$7
		{: RESULT = new LlamarFuncion($2,$4.toArray(new Expresion[$4.size()]),new Variable($6)); :}		
	|	MOSTRAR:$1 PARENTESIS_IZQUIERDO:$2 exp:$3 PARENTESIS_DERECHO:$4 PUNTO:$5
		{: RESULT = new Mostrar($3); :}
	|	MOSTRAR_LINEA:$1 PARENTESIS_IZQUIERDO:$2 exp:$3 PARENTESIS_DERECHO:$4 PUNTO:$5
		{: RESULT = new MostrarLinea($3); :}
	|	CREAR_VARIABLE:$1 ID:$2 SIM_TIPO:$3 TIPO:$4 PUNTO:$5
		{: RESULT = new Declaracion($2,$4); :}
	|	CREAR_LISTA:$1 ID:$2 SIM_TIPO:$3 TIPO:$4 CANTIDAD:$5 exp:$6 PUNTO:$7
		{: RESULT = new DeclaracionLista($2,$4,$6); :}	
	|	PONER:$1 exp:$2 EN:$3 ID:$4 POSICION:$5 exp:$6 PUNTO:$7 
		{: RESULT = new PonerLista($4,$6,$2); :}
;

assigExp ::=
		PONER:$1 exp:$2 EN:$3 ID:$4
		{: RESULT = new AsignacionExpresion(new Asignacion($4,$2)); :}
;

exp ::= 
		assigExp
	|	VERDADERO:$1 
		{: RESULT = new ValorVerdad(true); :}
	|	FALSO:$1 
		{: RESULT = new ValorVerdad(false); :}
	|
		NUM:$1 
		{: RESULT = new Numeral($1); :}
	|	ID:$1 
		{: RESULT = new Variable($1); :}
	|	STRING:$1
		{: RESULT = new Texto($1); :}
	|	exp:$1 SIGNO_MULT:$2 exp:$3 
		{: RESULT = new Multiplicacion($1, $3); :}
	|	exp:$1 SIGNO_MAS:$2 exp:$3 
		{: RESULT = new Suma($1, $3); :}
	|	exp:$1 SIGNO_MENOS:$2 exp:$3 
		{: RESULT = new Resta($1, $3); :}
	|	PARENTESIS_IZQUIERDO:$1 exp:$2 PARENTESIS_DERECHO:$3 
		{: RESULT = $2; :}
	|	exp:$1 SIGNO_IGUAL:$2 exp:$3 
		{: RESULT = new CompararIgual($1, $3); :}
	|	exp:$1 SIGNO_MENOR_IGUAL:$2 exp:$3 
		{: RESULT = new CompararMenorOIgual($1, $3); :}
	|	exp:$1 SIGNO_MAYOR_IGUAL:$2 exp:$3 
		{: RESULT = new CompararMayorOIgual($1, $3); :}
	|	exp:$1 SIGNO_MAYOR:$2 exp:$3 
		{: RESULT = new CompararMayor($1, $3); :}
	|	exp:$1 SIGNO_MENOR:$2 exp:$3 
		{: RESULT = new CompararMenor($1, $3); :}
	|	SIGNO_NO:$1 exp:$2 
		{: RESULT = new Negacion($2); :}
	|	exp:$1 SIGNO_Y:$2 exp:$3 
		{: RESULT = new Conjuncion($1, $3); :}
	|	exp:$1 SIGNO_O:$2 exp:$3 
		{: RESULT = new Disyuncion($1, $3); :}
	|	LARGO:$1 PARENTESIS_IZQUIERDO:$2 exp:$3 PARENTESIS_DERECHO:$4
		{: RESULT = new Largo($3); :}
	|	DEFINIDA:$1 PARENTESIS_IZQUIERDO:$2 ID:$3 PARENTESIS_DERECHO:$4
		{: RESULT = new Definida($3); :}	
	|	OBTENER_POSICION:$1 exp:$2 ID:$3
		{: RESULT = new ObtenerLista($3,$2); :}
;

repeat$stmt ::=
		
		{: List<Sentencia> $0;
		$0 = new ArrayList<Sentencia>();
		RESULT = $0; :}
	|	repeat$stmt:$1 stmt:$2 
		{: List<Sentencia> $0;
		$1.add($2); $0 = $1;
		RESULT = $0; :}
;

repeat$param ::=
		
		{: List<Parametro> $0;
		$0 = new ArrayList<Parametro>();
		RESULT = $0; :}
	|	repeat$param:$1 param:$2 
		{: List<Parametro> $0;
		$1.add($2); $0 = $1;
		RESULT = $0; :}
;

repeat$paramExp ::=
		
		{: List<Expresion> $0;
		$0 = new ArrayList<Expresion>();
		RESULT = $0; :}
	|	repeat$paramExp:$1 paramExp:$2 
		{: List<Expresion> $0;
		$1.add($2); $0 = $1;
		RESULT = $0; :}
;
